/* ----------------------------------------------------------- */
/* SIMPIMPFV2 - Simple Import File Example - CLLE version      */
/* ----------------------------------------------------------- */
/* Program.......... SIMPIMPFV2                                */
/* Author........... nick@nicklitten.com                       */
/* Calling Program.. *NONE                                     */
/* Calling Command.. *NONE                                     */
/* Function......... Enhanced version of SIMPIMPF, tidied to   */
/*                  my preferred CL template style             */
/* ----------------------------------------------------------- */
/* Modification History:                                       */
/* Author      Date       Description                          */
/* NickLitten  2025.02.26 V01 Created                          */
/* ----------------------------------------------------------- */
PGM

DCLPRCOPT LOG(*NO) DFTACTGRP(*NO) ACTGRP(*CALLER)

DCL VAR(&PGMNAME) TYPE(*CHAR) LEN(10)
DCL VAR(&CRASHED) TYPE(*LGL) VALUE('0')
DCL VAR(&MSGID) TYPE(*CHAR) LEN(7)
DCL VAR(&MSGDTA) TYPE(*CHAR) LEN(256)
DCL VAR(&MSGFIL) TYPE(*CHAR) LEN(10)
DCL VAR(&MSGFLIB) TYPE(*CHAR) LEN(10)
DCL VAR(&MSGKEY) TYPE(*CHAR) LEN(4)
DCL VAR(&RTNTYPE) TYPE(*CHAR) LEN(2)
DCL VAR(&SENDER) TYPE(*CHAR) LEN(80)

COPYRIGHT TEXT('SIMPIMPFV2 V01 Simple Import File Example')
DCL VAR(&COPYRIGHT) TYPE(*CHAR) LEN(256) +
  VALUE('Nick Litten Â© 2026 | IBM i V7.5 https://www.nicklitten.com')

DCL VAR(&COPYRIGHTP) TYPE(*PTR) STG(*DEFINED) DEFVAR(&COPYRIGHT)

MONMSG MSGID(CPF0000) EXEC(GOTO CMDLBL(CRASH)) /* Global monitor for any errors */

CALLSUBR SUBR(INZSR) /* a little initialization subroutine to do pre-run gubbins */

CALLSUBR SUBR(MAINLINE)

SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) +
 MSGDTA('Program' *BCAT &PGMNAME *TCAT 'completed normally') MSGTYPE(*COMP)

RETURN

CRASH: CALLSUBR SUBR(CRASH)


/* ------------------ */
/* SUBROUTINE SECTION */
/* ---------------------------------------------------------------------------------------------- */
/* Initialization Routine - could contains anything but lets use it to get the pgm name */
/* ---------------------------------------------------------------------------------------------- */
SUBR SUBR(INZSR)
  SNDPGMMSG MSG('What is this program name?') TOPGMQ(*SAME) MSGTYPE(*INFO) KEYVAR(&MSGKEY)
  RCVMSG PGMQ(*SAME) MSGTYPE(*INFO) WAIT(0) RMV(*YES) SENDER(&SENDER) SENDERFMT(*SHORT)
  CHGVAR VAR(&PGMNAME) VALUE(%SST(&SENDER 56 10))
ENDSUBR

/* ---------------------------------------------------------------------------------------------- */
/* MAINLINE - If you prefer you could use this for your mainline */
/* ---------------------------------------------------------------------------------------------- */
SUBR SUBR(MAINLINE)

CLRPFM NICKLITTEN/FLATFILE
MONMSG CPF3142 EXEC(RUNSQL SQL('create table nicklitten.FLATFILE (DATA varchar(140))') +
                    COMMIT(*NONE) NAMING(*SQL))

CLRPFM NICKLITTEN/CSVFILE
MONMSG CPF3142 EXEC(RUNSQL SQL('create table CSVFILE ( +
                                 INGREDIENT varchar(40), +
                                 QUANTITY varchar(10), +
                                 UNIT varchar(10), +
                                 CATEGORY varchar(20), +
                                 NOTES varchar(60) +
                               )') +
                    COMMIT(*NONE) NAMING(*SQL))

CPYFRMIMPF FROMSTMF('/home/nicklitten/ingredients.txt') +
           TOFILE(NICKLITTEN/FLATFILE)                  +
           MBROPT(*REPLACE)                             +
           RCDDLM(*CRLF)                                +
           DTAFMT(*DLM)                                 +
           STRDLM(*NONE)                                +
           RMVCOLNAM(*YES)

CPYFRMIMPF FROMSTMF('/home/nicklitten/ingredients.csv') +
           TOFILE(NICKLITTEN/CSVFILE)                   +
           MBROPT(*REPLACE)                             +
           RCDDLM(*CRLF)                                +
           DTAFMT(*DLM)                                 +
           RMVCOLNAM(*YES)

RTNSUBR

ENDSUBR

/* ---------------------------------------------------------------------------------------------- */
/* CRASH : Routine to handle unexpected errors */
/* ---------------------------------------------------------------------------------------------- */
SUBR SUBR(CRASH)
  IF COND(&CRASHED) THEN(RETURN)
  CHGVAR VAR(&CRASHED) VALUE('1')

/* This subroutine will read back through this programs joblog */
/* and pump out messages to your joblog based on the status */
/* codes returned from RCVMSG. RTNTYPE values include: */
/* - 01 Completion */
/* - 02 Diagnostic */
/* - 04 Information */
/* - 05 Inquiry */
/* - 06 Copy */
/* - 08 Request */
/* - 10 Request with prompting */
/* - 14 Notify (exception already handled at time of RCVMSG) */
/* - 15 Escape (exception already handled at time of RCVMSG) */
/* - 16 Notify (exception not handled at time of RCVMSG) */
/* - 17 Escape (exception not handled at time of RCVMSG) */
/* - 21 Reply, not checked for validity */
/* - 22 Reply, already checked for validity */
/* - 23 Reply, message default used */
/* - 24 Reply, system default used */
/* - 25 Reply, from System Reply List */
/* - 26 Reply, from exit program */
   RCVMSG MSGTYPE(*LAST) MSGDTA(&MSGDTA) MSGID(&MSGID) RTNTYPE(&RTNTYPE) +
          MSGF(&MSGFIL) SNDMSGFLIB(&MSGFLIB)
   IF COND(&RTNTYPE *EQ '15' *OR &RTNTYPE *EQ '17') THEN(DO) /* *escape */
      SNDPGMMSG MSGID(&MSGID) MSGF(&MSGFIL) MSGDTA(&MSGDTA) MSGTYPE(*DIAG)
   ENDDO
   SNDPGMMSG MSGID(CPF9898) MSGF(QCPFMSG) +
             MSGDTA('* Program' *BCAT &PGMNAME *BCAT 'ended abnormally') +
             MSGTYPE(*ESCAPE)
ENDSUBR
ENDPGM: ENDPGM